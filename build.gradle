plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'maven-publish'
    id 'idea'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2.2'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "edu.wpi.first.ntcore:ntcore-java:2019.+"
    compile "edu.wpi.first.wpiutil:wpiutil-java:2019.+"
    compile 'junit:junit:4.12'
    compile 'jfree:jcommon:1.0.16'
    compile 'jfree:jfreechart:1.0.13'
    runtime "edu.wpi.first.ntcore:ntcore-jni:2019.+:all"
}

jar {
    baseName = 'SmartDashboard'
}

shadowJar {
    baseName = 'SmartDashboard'
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                classifier null
            }
            groupId 'edu.wpi.first.wpilib'
            artifactId 'SmartDashboard'
            version WPILibVersion.version
        }
    }
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = '6.19'
    if (project.hasProperty("ignoreCheckstyle")) {
        ignoreFailures = true
    }
}

mainClassName = "edu.wpi.first.smartdashboard.SmartDashboard"

project(':fakeRobot') {
  apply plugin: 'java'
  apply plugin: 'application'

  dependencies {
    compile "edu.wpi.first.ntcore:ntcore-java:2019.+"
    compile "edu.wpi.first.wpiutil:wpiutil-java:2019.+"
    runtime "edu.wpi.first.ntcore:ntcore-jni:2019.+:all"
  }

  mainClassName = "edu.wpi.livewindowfakerobot.LiveWindowFakeRobot"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

wrapper {
    gradleVersion = '4.10.2'
}
